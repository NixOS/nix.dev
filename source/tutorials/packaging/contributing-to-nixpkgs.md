---
myst:
  html_meta:
    "description lang=en": "Contributing to Nixpkgs"
    "keywords": "Nix, Nixpkgs, contributing"
---


(contributing-to-nixpkgs)=

# Contributing to Nixpkgs

[Nixpkgs](https://github.com/nixos/nixpkgs) is one of the largest public repositories by contributions and *the* largest single collection of software packages available on the web.

In this tutorial, which follows {ref}`the tutorial on packaging existing software <packaging-existing-software>`, you will learn how to prepare newly-packaged software for submission to Nixpkgs, becoming a Nixpkgs contributor in the process.

## Tags, Revisions, and Releases
An earlier version of the `icat.nix` definition created in the {ref}`Packaging Existing Software <packaging-existing-software>` tutorial used the `master` revision of the [upstream `icat` repository](https://github.com/atextor/icat), which would be suitable for individual use but isn't good practice for submission to Nixpkgs.

It is much better to use a fixed revision corresponding to a particular release version of the software so maintainers (perhaps you!) can easily check whether the package should be updated.

This was already done in the previous tutorial, but as a reminder, here are the steps:
1. Check the upstream repository for tags or releases. On GitHub, these are available at `https://github.com/<owner>/<repo>/tags` and `https://github.com/<owner>/<repo>/releases` respectively.
2. Choose the latest tag or release and write it into a string literal as the value of the `rev` attribute passed to the [fetcher](https://nixos.org/manual/nixpkgs/stable/#chap-pkgs-fetchers).
3. Use `nix-prefetch-url` or a similar tool to fetch, unpack, and hash the files marked by the tag or release you selected.
4. Alternatively, you can also use a tool such as `nix-init` to automatically generate a required derivation for some common project types, or use its output to help lay down a skeleton implementation of the above steps.

In the `icat` example, the source was hosted on GitHub, so `fetchFromGitHub` was used for the `src` attribute passed to `stdenv.mkDerivation`, with `rev = "v0.5"` corresponding to the latest tag, and a SHA256 hash generated by

```console
nix-prefetch-url --unpack https://github.com/atextor/icat/archive/refs/tags/v0.5.tar.gz --type sha256`
```

Here is the final version of `icat.nix` from the previous tutorial:

```nix
# icat.nix
{ lib
, stdenv
, fetchFromGitHub
, imlib2
, xorg
}:

stdenv.mkDerivation {
  pname = "icat";
  version = "0.5";

  src = fetchFromGitHub {
    owner = "atextor";
    repo = "icat";
    rev = "v${version}";
    sha256 = "0wyy2ksxp95vnh71ybj1bbmqd5ggp13x3mk37pzr99ljs9awy8ka";
  };

  buildInputs = [ imlib2 xorg.libX11.dev ];

  installPhase = ''
    runHook preInstall
    mkdir -p $out/bin
    cp icat $out/bin
    runHook postInstall
  '';
}
```

And here is a `default.nix` that makes the derivation in `icat.nix` available to `nix-build`:

```nix
# default.nix
let
  pkgs = import <nixpkgs> { };
in
{
  icat = pkgs.callPackage ./icat.nix { };
}
```

Copy both of `icat.nix` and `default.nix` onto your own machine, and then verify that you can build the derivation:

```console
$ nix-build -A icat
...
/nix/store/g6w508vxwr3df25dnl4k3xvcr4pqxprj-icat
```

The (omitted) output indicates that the package successfully built, and the very last line reports where Nix put the build result.

## Phases and Hooks
At the end of the previous tutorial, you learned that Nix derivations can be decomposed into a sequence of [phases](https://nixos.org/manual/nixpkgs/stable/#sec-stdenv-phases), each of which controls some aspect of the build process.

You also learned about the existence of [*hooks*](https://nixos.org/guides/nix-pills/basic-dependencies-and-hooks), shell functions which execute in some of the derivation phases to control the build *environment*.

Finally, you used this new knowledge to update the `installPhase` of the `icat` derivation with the pre- and post-install hooks:

```diff
# icat.nix
...
  installPhase = ''
+   runHook preInstall
    mkdir -p $out/bin
    cp icat $out/bin
+   runHook postInstall
'';
...
```

A number of other hooks are available, and when packaging for Nixpkgs you should remember to include calls to these hooks in the phases you define, to make it easy for other contributors to [override](https://nixos.org/manual/nixpkgs/stable/#chap-overrides) parts of your derivations in the future.

## Package Metadata
The Nixpkgs [contribution guidelines](https://nixos.org/manual/nixpkgs/stable/#part-contributing) require all packages in Nixpkgs to have a `meta` attribute in their derivation, which contains information like a description of the package, the homepage of the project it belongs to, the software license, which platforms the package can be built for, and a list of Nixpkgs maintainers for the package.

Usually, the required metadata can be found in the upstream repository.

For `icat`, add the following `meta` attribute to the attrset passed to `stdenv.mkDerivation` in `icat.nix`:

```nix
# icat.nix
...
  meta = with lib; {
    description = "icat (Image cat) outputs images in 256-color capable terminals.";
    homepage = "https://github.com/atextor/icat";
    license = licenses.bsdOriginal;
    platforms = platforms.unix;
  };
...
```

With no further technical changes necessary, this derivation is now complete.

## Adding to Nixpkgs
You can now clone a local copy of Nixpkgs, and move your derivation into an appropriate location within that source tree.

```console
$ git clone https://github.com/nixos/nixpkgs
```

`icat` is a console tool which also displays graphics, so there are a few places within Nixpkgs that you could reasonably move it.

In the past, packages were contributed based on the category they might fit in.

For example: there are currently two other similar tools already in Nixpkgs, both of which are stored in `pkgs/applications/graphics`, so that might have been a good home for `icat` too.

However, [Nixpkgs now prefers organization of packages by the the two-letter prefix of their package name](https://github.com/NixOS/nixpkgs/tree/master/pkgs/by-name#readme) within the `pkgs/by-name/` directory, [when possible](https://github.com/NixOS/nixpkgs/tree/master/pkgs/by-name#limitations).

This eliminates the need to decide upon a particular category if a package fits into multiple, and it also eliminates the requirement of [updating `all-packages.nix` when using the category-based hierarchy](https://github.com/NixOS/nixpkgs/blob/master/pkgs/README.md#versioning), and since our case isn't bound by any naming limitations, let's use the two-letter prefix system:

```console
$ mkdir nixpkgs/pkgs/by-name/ic/icat
```

You should rename `icat.nix` when you copy it to its new home, to keep to the convention of package derivation files [being named `package.nix`](https://github.com/NixOS/nixpkgs/tree/master/pkgs/by-name#example) when using the two-letter prefix system:

```console
$ mv icat.nix nixpkgs/pkgs/applications/graphics/icat/default.nix
```

:::{note}
Other organization systems might specify a different standard name for the derivation root. For example, the [category hierarchy](https://github.com/NixOS/nixpkgs/blob/master/pkgs/README.md#category-hierarchy) system requires the standard name `default.nix`.

Additionally, as alluded to above, other hierarchy systems might require additional steps.

For example, when using the categorical hierarchy, the relative directory paths to all packages get referenced in `pkgs/top-level/all-packages.nix`, which you would also need to update, putting the new package next to the other similar tools:

```diff
# all-packages.nix
...
+ icat = callPackage ../applications/graphics/icat { };

  imgcat = callPackage ../applications/graphics/imgcat { };

  img-cat = callPackage ../applications/graphics/img-cat { };
...
```

:::{note}
Attributes in `all-packages.nix` are arranged *approximately* in alphabetical order, either by attribute name or by the directory containing their corresponding `default.nix`.
:::
:::

## Two Final Tests: Build and Run
To confirm that the package builds within its new context, you should change directories to the root of the Nixpkgs repo and run `nix-build -A icat` a final time:

```console
$ cd nixpkgs
$ nix-build -A icat
this derivation will be built:
  /nix/store/kf7na78dhwhfrgxjaj27gjkqs4b3n7wz-icat.drv
...
/nix/store/vr2vk8z8839l5j6gra0qlyxrh5sarmh4-icat
```

The build completed as expected, and produced a `result` in the current directory. So as a sanity check, we ought to confirm that the program does run on our system.

```console
$ nix-shell --run
```

Assuming that the package builds and run on our machine, it is now ready for submission to Nixpkgs!

## Next Steps

All that remains is to make sure your familiarise yourself with the conventions of [contributing to Nixpkgs](https://github.com/NixOS/nixpkgs/blob/master/CONTRIBUTING.md) and the conventions defining [package maintainers](https://github.com/NixOS/nixpkgs/tree/master/maintainers#readme).

:::{note}
Before you submit your package, you must:
* ensure you are [registered](https://github.com/NixOS/nixpkgs/tree/master/maintainers#how-to-become-a-maintainer) as a maintainer by listing yourself in [`nixpkgs/maintainers/maintainers-list.nix`](https://github.com/NixOS/nixpkgs/blob/master/maintainers/maintainer-list.nix).
* ensure you add yourself to the list of maintainers for the package you're about to submit, using the name you set in `maintainer-list.nix`:

```diff
# icat.nix
...
  meta = with lib; {
    ...
+   maintainers = [ maintainers.<your_name> ];
    ...
  };
...
```
:::
